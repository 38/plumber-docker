#!/bin/env pscript
/**
 * Copyright (C) 2017-2018, Hao Hou
 **/
/* The main script for the static file server */

// Load the configuration
import("service");
import("servlet");
import("/fileserver/config");

print("Plumber Static File Server is serving files under directory " + server_base_dir);

//define the file server
fileserver = {
	RequestParser      := "network/http/parser";
	FileReader         := Servlet.init("filesystem/readfile", {
		"root"              : server_base_dir,       /* The default directory */
		"mime-map-file"     : mime_type_file,        /* The MIME type spec, using apache format */
		"input-mode"        : "http",                /* Takes an HTTP request */
		"output-mode"       : "http",                /* Produces an HTTP response */
		"range-access"      : undefined,             /* Allow access a range */
		"compressable"      : "text/*,application/*javascript*,application/*xml*,application/*json*"   	
	});
	ResponseGenerator  := "network/http/render --chunked --gzip --deflate";

	/* Setup normal logic */
	() -> "input" RequestParser {
		"protocol_data" -> "protocol_data";
		"default" -> "request" FileReader "file" -> "response";
	} ResponseGenerator "output" -> ();

	/* Let's handle the unexpected service error */
	{
		RequestParser    "__error__" -> "500";
		FileReader       "__error__" -> "500";
	} ResponseGenerator;
};

Service.start(fileserver);
