#!/bin/env pscript
/**
 * Copyright (C) 2017-2018, Hao Hou
 **/
/* The main script for the static file server */

// Load the configuration
import("service");
import("servlet");
import("/fileserver/config");

//define the file server
fileserver = {
	RequestParser      := "getpath";
	FileReader         := Servlet.init("filesystem/readfile",{
		"root"           : server_base_dir,
		"mime-map-file"  : mime_type_file,
		"input-mode"     : "string",
		"output-mode"    : "http",
		"default-index"  : yes,
		"compressable"   : "application/*xml*,application/*javascript*,text/*"
	});
	ResponseGenerator  := "network/http/render --gzip --deflate --chunked";

	/* Setup normal logic */
	() -> "request" RequestParser {
		"path" -> "path" FileReader "file" -> "response";
		"accept_encoding" -> "accept_encoding";
	} ResponseGenerator "output" -> ();

	/* Let's handle the unexpected service error */
	{
		FileReader    "__error__" -> "500";
	} ResponseGenerator;
};

runtime.daemon.id = "plumber-server";

/* (Optional) dump the visualization graph */
Service.visualize(fileserver);

/* start the file server */
Service.start(fileserver);
